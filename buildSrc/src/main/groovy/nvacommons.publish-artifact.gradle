plugins {
    id 'maven-publish'
    id 'signing'
}

/***********************************ATTENTION*******************************************************
 Base plugin for publishing artifacts. Contains shared functionality like signing and POM configuration.
 Part of the maven publishing configuration has to be in the root project (nvacommons.root.gradle)
 ************************************ ATTENTION*****************************************************/

signing {
    required = { project.hasProperty("signingKey") }
    useInMemoryPgpKeys(findProperty('signingKey'),
            findProperty('signingPassword'))
    sign publishing.publications
}

ext.configureCommonPom = { MavenPom pom ->
    pom.name.set(project.name)
    pom.description.set('A common library for the NVA project')
    pom.url.set('https://github.com/BIBSYSDEV/nva-commons')
    pom.licenses {
        license {
            name.set('MIT License')
            url.set('http://www.opensource.org/licenses/mit-license.php')
        }
    }
    pom.developers {
        developer {
            name.set('Orestis Gkorgkas')
            email.set('og@unit.no')
            organization.set('UNIT')
            organizationUrl.set('https://www.unit.no/')
        }
    }
    pom.scm {
        connection.set('scm:git:git://github.com/BIBSYSDEV/nva-commons.git')
        developerConnection.set('scm:git:ssh://github.com/BIBSYSDEV/nva-commons.git')
        url.set('https://github.com/BIBSYSDEV/nva-commons/tree/main')
    }
}

afterEvaluate {
    tasks.named('publishToMavenLocal') {
        dependsOn(gradle.includedBuild('build-logic').task(':publishToMavenLocal'))
    }
    tasks.named("publishToSonatype") {
        dependsOn(gradle.includedBuild("build-logic").task(":publishToSonatype"))
    }
}
