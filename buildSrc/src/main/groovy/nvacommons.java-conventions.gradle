plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'idea'
}

group 'com.github.bibsysdev'
version = '1.31.1'


sourceCompatibility = JavaVersion.VERSION_17  // source-code version and must be <= targetCompatibility
targetCompatibility = JavaVersion.VERSION_17 // bytecode target version


repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

test {
    useJUnitPlatform {}
    failFast = true
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.required
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
    pmdMain {}
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) {
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

dependencies {
    api(libs.lambda.log4j) {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: "com.amazonaws", module: "aws-lambda-java-core"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'

    }

    compileOnly libs.apiguardian
}

