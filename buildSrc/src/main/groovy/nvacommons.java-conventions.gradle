plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'checkstyle'
    id 'jacoco-report-aggregation'
    id 'pmd'
    id 'idea'
}

group 'com.github.bibsysdev'
version = '1.41.2'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

testCodeCoverageReport {
    reports {
        csv.required = true
        xml.required = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
    failFast = true
    testLogging {
        events 'skipped', 'passed', 'failed'
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
    pmdMain {}
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {

    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}

test {
    useJUnitPlatform {}
    failFast = true
    finalizedBy(jacocoTestReport)
}

jacocoTestReport {
    dependsOn(test)
    reports {
        xml.required
    }
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

tasks.withType(Checkstyle) .configureEach{
    reports {
        xml.required
        html.required
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

dependencies {
    api(libs.lambda.log4j) {
        exclude group: "org.apache.logging.log4j", module: "log4j-core"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
        exclude group: "com.amazonaws", module: "aws-lambda-java-core"
        exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
    }

    compileOnly libs.apiguardian
}

