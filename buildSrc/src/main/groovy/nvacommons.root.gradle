plugins {
    id 'io.github.gradle-nexus.publish-plugin'
    id 'nvacommons.gradlelint'
    id 'nvacommons.java-conventions'
    id 'nvacommons.publish-maven'
    id 'jacoco-report-aggregation'
    id 'version-catalog'
}

//workaround for jacoco-merge to work
allprojects {
    apply plugin: 'nvacommons.java-conventions'
}

publishing {
    publications {
        // Publish the version catalog from the root project as an artifact
        versionCatalog(MavenPublication) {
            artifactId = 'versions'
            from components.versionCatalog
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty("sonatypeUsername")
            password = findProperty("sonatypePassword")
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }

    clientTimeout = Duration.ofSeconds(600)
    connectTimeout = Duration.ofSeconds(60)
}

dependencies {
    subprojects.forEach {
        jacocoAggregation it
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
        integrationTestCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.INTEGRATION_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
    dependsOn tasks.named('integrationTestCodeCoverageReport', JacocoReport)
}