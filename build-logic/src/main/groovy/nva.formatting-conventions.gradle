plugins {
    id 'com.diffplug.spotless'
    id 'nva.configuration'
}

spotless {
    enforceCheck = false

    if (plugins.hasPlugin('java')) {
        java {
            toggleOffOn() // Ignores sections between `spotless:off` / `spotless:on`
            googleJavaFormat().reflowLongStrings().formatJavadoc(true).reorderImports(true)
        }
    }

    groovyGradle {
        target '**/*.gradle'
        greclipse()
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }

    format 'misc', {
        target '.gitignore', '.gitattributes', '.editorconfig', '**/*.md'
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
    }
}

afterEvaluate {
    if (nva.spotlessEnabled) {
        tasks.named('build').configure {
            dependsOn('spotlessApply')
        }

        tasks.matching { it.name == 'test' }.configureEach {
            dependsOn('spotlessApply')
        }
    }
    if (nva.spotlessEnforced) {
        spotless.enforceCheck = true
    }
}
