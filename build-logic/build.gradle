plugins {
    id 'groovy-gradle-plugin'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.nexus.publish)
}

// Helper to turn a version‑catalog plugin alias into marker‑artifact coordinates <id>:<id>.gradle.plugin:<version>
def pluginNotation = { Provider<PluginDependency> plugin ->
    plugin.map {
        "${it.pluginId}:${it.pluginId}.gradle.plugin:${it.version.getStrictVersion()}"
    }
}

dependencies {
    implementation(pluginNotation(libs.plugins.errorprone))
    implementation(pluginNotation(libs.plugins.gradle.versions))
    implementation(pluginNotation(libs.plugins.nebula.lint))
    implementation(pluginNotation(libs.plugins.nexus.publish))
    implementation(pluginNotation(libs.plugins.spotless))
}

group = gradle.parent.rootProject.getProperty('GROUP')
version = gradle.parent.rootProject.getProperty('VERSION_NAME')

// Configure publishing (included build doesn't inherit root config)
apply from: "${rootDir}/../gradle/shared-publish.gradle"
publishing {
    publications.withType(MavenPublication).configureEach {
        artifactId = project.name
        pom(configureCommonPom)
    }
}

signing {
    required = { gradle.parent.rootProject.hasProperty("signingKey") }
    useInMemoryPgpKeys(gradle.parent.rootProject.findProperty('signingKey'),
            gradle.parent.rootProject.findProperty('signingPassword'))
    sign publishing.publications
}

nexusPublishing {
    repositories { sonatype() }
}
