import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'nebula.lint' version '16.8.0'
}


sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

gradleLint {
    rules = ['unused-dependency']
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.2.2"
}


repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}


def jacksonVersion = "2.10.3"
def awsSdkVersion = "1.11.774"
def jupiterVersion = "5.6.2"
def log4jVersion = "2.13.2"


apply plugin: 'java-library'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'jacoco'
apply plugin: 'checkstyle'
apply plugin: 'pmd'

dependencies {
    implementation group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1'
    runtimeOnly group: 'com.amazonaws', name: 'aws-lambda-java-events', version: '2.2.8'
    runtimeOnly group: 'com.amazonaws', name: 'aws-java-sdk-lambda', version: awsSdkVersion
    implementation('org.zalando:problem:0.24.0') {
        dependencies {
            compileOnly(group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.0') {
                because 'Dependency has maven scope provided'
            }
        }
    }
    implementation group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.24.0'
    runtimeOnly group: 'com.amazonaws', name: 'aws-java-sdk-api-gateway', version: awsSdkVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: jacksonVersion
    runtimeOnly group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonVersion
    runtimeOnly group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.11'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.9'

    testImplementation group: 'com.github.BIBSYSDEV', name: 'nva-testutils', version: '0.1.12'

    testImplementation(group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4') {
        because 'Scope provided in junit-jupiter'
    }

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jupiterVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: jupiterVersion
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jupiterVersion


    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.4.4'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    // If one is using nva-commons, api allows the re-usage of these packages.

    gradleLint.ignore ('unused-dependency') {
        api group: 'com.amazonaws', name: 'aws-lambda-java-log4j2', version: '1.1.1'
        api group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
        api group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
        api group: 'org.apache.logging.log4j', name: 'log4j-slf4j18-impl', version: log4jVersion
        api(group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4') {
            because "Because we use the library directly to define loggers in our code"
        }
    }
}

test {
    useJUnitPlatform {
    }
    failFast = true
    finalizedBy jacocoTestReport
    testLogging {
        events 'failed', 'passed', 'skipped'
    }
    Map<String, String> envVariables = new HashMap<>()
    envVariables.put("EXISTING_NON_EMPTY_VARIABLE", "SomeContent")
    envVariables.put("EXISTING_EMPTY_VARIABLE", "  ")
    environment(envVariables)
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

pmd {
    ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
    ruleSets = []
    ignoreFailures = false
}

checkstyle {
    configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
    showViolations = true
}

shadowJar {
// Transformation for merging Log4j2Plugins.dat files.
    transform(Log4j2PluginsCacheFileTransformer)
}


tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
        html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

check.dependsOn jacocoTestCoverageVerification
jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'METHOD'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }

        rule {
            limit {
                counter = 'CLASS'
                value = 'COVEREDRATIO'
                minimum = 1.000
            }
        }
    }
}
