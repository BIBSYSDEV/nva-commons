plugins {
    id 'java-library'
    id 'jacoco'
    id 'base'
    id 'checkstyle'
    id 'pmd'
    id 'nebula.lint' version '16.17.0'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.0.0'
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "5ba11e4895739"
        }
    }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "6.7"
}

allprojects {
    group = 'com.github.bibsysdev'
    version = '1.5.16'

    apply plugin: 'java-library'
    apply plugin: 'jacoco'
    apply plugin: 'base'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'nebula.lint'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    gradleLint {
        rules = ['unused-dependency']
    }

    project.ext {
        jacksonVersion = "2.12.0"
        awsSdkVersion = "1.11.931"
        awsSkd2Version = '2.16.37'
        jupiterVersion = "5.7.0"
        log4jVersion = "2.13.2"
        nvaTestUtilsVersion = "0.1.16"
    }

    test {
        useJUnitPlatform {
            excludeTags("RemoteTest")
        }
        failFast = true
        finalizedBy jacocoTestReport
        testLogging {
            events 'failed', 'passed', 'skipped'
        }

        Map<String, String> envVariables = new HashMap<>()
        envVariables.put("EXISTING_NON_EMPTY_VARIABLE", "SomeContent")
        envVariables.put("EXISTING_EMPTY_VARIABLE", "  ")
        environment(envVariables)
    }

    pmd {
        ruleSetConfig = rootProject.resources.text.fromFile('config/pmd/ruleset.xml')
        ruleSets = []
        ignoreFailures = false
    }

    checkstyle {
        configFile = rootProject.resources.text.fromFile('config/checkstyle/checkstyle.xml').asFile()
        showViolations = true
    }

    tasks.withType(Checkstyle) {
        reports {
            xml.enabled false
            html.enabled true
            html.stylesheet rootProject.resources.text.fromFile('config/checkstyle/checkstyle-simple.xsl')
        }
    }

    check.dependsOn jacocoTestCoverageVerification
    jacocoTestCoverageVerification.dependsOn(jacocoTestReport)

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'METHOD'
                    value = 'COVEREDRATIO'
                    minimum = 1.000
                }
            }

            rule {
                limit {
                    counter = 'CLASS'
                    value = 'COVEREDRATIO'
                    minimum = 1.000
                }
            }
        }
    }
}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    dependencies {
        // This method contains the logger class for AWS Lambda functions.
        api(group: 'com.amazonaws', name: 'aws-lambda-java-log4j2', version: '1.1.1') {
            exclude group: "org.apache.logging.log4j", module: "log4j-core:2.8.2"
            exclude group: 'org.apache.logging.log4j', module: 'log4j-api'
            exclude group: "com.amazonaws", module: "aws-lambda-java-core"
            exclude group: 'org.apache.logging.log4j', module: 'log4j-core'
        }

        api(group: 'com.amazonaws', name: 'aws-lambda-java-core', version: '1.2.1') {
            exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
            exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
            exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
            exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jsr310"
            exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
            exclude group: "org.apache.logging.log4j", module: "log4j-core:2.8.2"
        }
        api(group: 'org.zalando', name: 'jackson-datatype-problem', version: '0.25.0') {
            exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
            exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
            exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
            exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jsr310"
            exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        }

        api group: 'org.apache.logging.log4j', name: 'log4j-core', version: project.ext.log4jVersion
        api group: 'org.apache.logging.log4j', name: 'log4j-api', version: project.ext.log4jVersion
        api group: 'org.apache.logging.log4j', name: 'log4j-slf4j18-impl', version: project.ext.log4jVersion
        api(group: 'org.slf4j', name: 'slf4j-api', version: '1.8.0-beta4') {
            because 'Because we use the library directly to define loggers in our code'
        }

        api group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: project.ext.jacksonVersion
        api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: project.ext.jacksonVersion
        api group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: project.ext.jacksonVersion
        api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: project.ext.jacksonVersion
        api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: project.ext.jacksonVersion

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: project.ext.jupiterVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: project.ext.jupiterVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: project.ext.jupiterVersion

        compileOnly(group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.1') {
            because("junit.jupiter version 5 depends on it and testImplementation seems not enough")
        }

        testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.8.0'
        testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
        testImplementation group: 'org.hamcrest', name: 'hamcrest-core', version: '2.2'
        testImplementation group: 'com.github.npathai', name: 'hamcrest-optional', version: '2.0.0'
        testImplementation group: 'com.github.BIBSYSDEV', name: 'nva-testutils', version: '0.1.14'

    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = project.name
                from components.java
                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
                pom {
                    name = project.name
                    description = 'A commons library for the NVA project'
                    url = 'https://github.com/BIBSYSDEV/nva-commons'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    developers {
                        developer {
                            name = 'Orestis Gkorgkas'
                            email = 'og@unit.no'
                            organization = 'UNIT'
                            organizationUrl = 'https://www.unit.no/'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/BIBSYSDEV/nva-commons.git'
                        developerConnection = 'scm:git:ssh://github.com/BIBSYSDEV/nva-commons.git'
                        url = 'https://github.com/BIBSYSDEV/nva-commons/tree/main'
                    }
                }
            }
        }
    }

    signing {
        if (isMainBranch()) {
            useInMemoryPgpKeys(findProperty("signingKey"), findProperty("signingPassword"))
            sign publishing.publications.mavenJava
        }

    }
}

def gitBranch() {
    def branch = ""
    def proc = "git rev-parse --abbrev-ref HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    branch
}

boolean isMainBranch() {
    def branch = gitBranch()
    return ((branch == "master" || branch == "main"))
}


def getProjectList() {
    // These projects are considered. Replace with a different list as needed.
    subprojects + project
}

task jacocoMerge(type: JacocoMerge) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Merge the JaCoCo data files from all subprojects into one'
    project.afterEvaluate {
        // do it at the end of the config phase to be sure all information is present
        FileCollection execFiles = project.objects.fileCollection()   // an empty FileCollection
        getProjectList().each { Project subproject ->
            if (subproject.pluginManager.hasPlugin('jacoco')) {
                def testTasks = subproject.tasks.withType(Test)
                dependsOn(testTasks)   // ensure that .exec files are actually present

                testTasks.each { Test task ->
                    // The JacocoTaskExtension is the source of truth for the location of the .exec file.
                    JacocoTaskExtension extension = task.getExtensions().findByType(JacocoTaskExtension.class)
                    if (extension != null) {
                        execFiles.from extension.getDestinationFile()
                    }
                }
            }
        }
        executionData = execFiles
    }
    doFirst {
        // .exec files might be missing if a project has no tests. Filter in execution phase.
        executionData = executionData.filter { it.canRead() }
    }
}

def getReportTasks(JacocoReport pRootTask) {
    getProjectList().collect {
        it.tasks.withType(JacocoReport).findAll { it != pRootTask }
    }.flatten()
}

task jacocoRootReport(type: JacocoReport, dependsOn: tasks.jacocoMerge) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Generates an aggregate report from all subprojects'

    logger.lifecycle 'Using aggregated file: ' + tasks.jacocoMerge.destinationFile
    executionData.from tasks.jacocoMerge.destinationFile

    project.afterEvaluate {
        // The JacocoReport tasks are the source of truth for class files and sources.
        def reportTasks = getReportTasks(tasks.jacocoRootReport)
        classDirectories.from project.files({
            reportTasks.collect { it.classDirectories }.findAll { it != null }
        })
        sourceDirectories.from project.files({
            reportTasks.collect { it.sourceDirectories }.findAll { it != null }
        })
    }

    reports {
        xml.enabled true
    }
}

build {
    dependsOn jacocoRootReport
}
